plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id "maven-publish"
    id 'java'
    id 'idea'
}

group = project.group
version = project.version

repositories {
    mavenCentral()
}

//using java 8 for compatibility with old projects
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

dependencies {
    implementation gradleApi()
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

gradlePlugin {
    website = "https://github.com/Srdjan-V/LocalGitDependency"
    vcsUrl = "https://github.com/Srdjan-V/LocalGitDependency"

    plugins {
        localGitDependency {
            displayName = "Local Git Dependency Plugin"
            description = "Gradle plugin to clone projects with git and build them"
            tags.set(["git", "dependency"])

            id = "io.github.srdjanv.local-git-dependency"
            implementationClass = "io.github.srdjanv.localgitdependency.LocalGitDependencyPlugin"
        }

        modelInjection {
            displayName = "Local Git Dependency Model Injection Plugin"
            description = "Helper plugin for local git dependency"
            tags.set(["model", "git", "dependency"])

            id = "io.github.srdjanv.model-injection"
            implementationClass = "io.github.srdjanv.localgitdependency.injection.plugin.ModelInjectionPlugin"
        }

    }
}

import org.apache.tools.ant.filters.ReplaceTokens
task filterTokens(type: Sync) {

    inputs.property 'version', version

    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [PROJECTVERSION: version.toString()])
    into "$buildDir/src/$sourceSets.main.name/$sourceSets.main.java.name"
}

compileJava.source = filterTokens.outputs

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = 'local-git-dependency'
        }
    }
}
