apply {
    plugin "maven-publish"
    plugin 'java-gradle-plugin'
    plugin 'java'
    plugin 'idea'
}
group = project.group
version = project.version

repositories {
    mavenCentral()
}

//using java 8 for compatibility with old projects
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

dependencies {
    implementation gradleApi()
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
}

tasks.named('test') {
    useJUnitPlatform()
}

gradlePlugin {
    website.set("https://github.com/Srdjan-V/LocalGitDependency")
    vcsUrl.set("https://github.com/Srdjan-V/LocalGitDependency")

    plugins {
        localGitDependency {
            displayName = "Local Git Dependency Plugin"
            description = "Gradle plugin to clone projects with git and build them"
            tags.set(listOf("git", "dependency"))

            id = 'com.srdjanv.local-git-dependency'
            implementationClass = 'com.srdjanv.localgitdependency.LocalGitDependencyPlugin'
        }

        modelInjection {
            displayName = "Local Git Dependency Model Injection Plugin"
            description = "Helpper plugin for local git dependency"
            tags.set(listOf("model", "git", "dependency"))

            id = "com.srdjanv.model-injection"
            implementationClass = "com.srdjanv.localgitdependency.injection.plugin.ModelInjectionPlugin"
        }

    }
}

import org.apache.tools.ant.filters.ReplaceTokens
task filterTokens(type: Sync) {

    inputs.property 'version', version

    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [PROJECTVERSION: version.toString()])
    into "$buildDir/src/$sourceSets.main.name/$sourceSets.main.java.name"
}

compileJava.source = filterTokens.outputs

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = 'local-git-dependency'
        }
    }
}
